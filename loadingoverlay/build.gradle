apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = 1.8
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.airbnb.android:lottie:3.4.0'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
    ext {
        libraryVersion = '0.0.3'
        libraryName = 'loadingoverlay'
        publishedGroupId = 'com.mohamedabulgasem'
        libraryDescription = 'A customisable loading overlay for Android.'
        siteUrl = 'https://github.com/MohamedAbulgasem/LoadingOverlay'
        gitUrl = 'https://github.com/MohamedAbulgasem/LoadingOverlay.git'
    }
}

group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller.pom.project {
        name libraryName
        description libraryDescription
        packaging 'aar'
        url siteUrl
        groupId publishedGroupId
        artifactId libraryName

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id 'mohamedabulgasem'
                name 'Mohamed Abulgasem'
                email 'algiriany93@gmail.com'
            }
        }
        scm {
            url siteUrl
            connection gitUrl
            developerConnection gitUrl
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// Configure gradle-bintray-plugin (for publishing releases)
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    publish = true

    pkg {
        repo = 'maven'
        name = libraryName
        desc = libraryDescription
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/MohamedAbulgasem/LoadingOverlay/issues'
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}